!THIS ONE
!! # Noun (Gaeilge) Replace Rules
!! Elaine Uí Dhonnchadha
!! 12/12/96 - continued March 1998
! 
!****************************************************************************
!! ## INTRO
!! Certain vowel combinations will be represented by one symbol as these vowel
!! sounds should be treated as a unit.
!!
!! ### DIPHTHONG EXAMPLE
!! For example in slenderising "ua" we want "uai" not "uiai".
!! Diphthongs 
!!	- ua -> U eg bruas ^Caol, buairt ^Lea, díomuach ^Ath (adj.)
!!	- ia -> I eg scian ^Lea	(NOT IN USE AT PRESENT)
!!
!! ### LONG VOWEL EXAMPLE
!! In cathair (city) the "ai" is syncopated but in cathaoir (chair) "aoi" is not.
!! There are two other diphthongs in Irish but the are not explicitly represented
!! orthographically (radharc, etc see Fpóca)
!!
!! ### Long Vowels
!!	ao -> O saol, gaol, cathaoir (sounds like ee)
!!	ae -> E grosaeir, traen ^Lea (sounds like ay)
!!
!!	Note: sometimes "eo" has a long sound (like ó) eg ceol, gleo
!***************************************************************************
!! ## Rules 
!! ### Definitions
define Vowel a|e|i|o|u|á|é|í|ó|ú|%^AO|%^IA|%^AE|%^UA ;		 !!≈ - @CODE@ 
define Nountag  %^F|%^M|%^C|%^G|%^V ;		 !!≈ - @CODE@ 
define Cons  b|c|d|f|g|h|j|k|l|m|n|p|q|r|s|t|v|w|x|y|z|%^FC|%^FY|%^FS|%^FB ;		 !!≈ - @CODE@ 

!! Regex
read regex [ a e -> %^AE ]			!!≈ - @CODE@  Long vowel symbol
!.o.
![ i a -> %^IA ]					!!≈ - @CODE@  Diphthong symbol

.o.
[ a o -> %^AO ]						!!≈ - @CODE@  Long vowel symbol

.o.
[ u a -> %^UA ]						!!≈ - @CODE@  Diphthong symbol
! ;
! (c) January 2001
! (1) Coimriú : Syncopation of Last Unstressed Syllable (^Coim)
!		before an ending is added
!***************************************************************************!
! SYNCOPATION
!
! Vowel(s) in last syllable is/are dropped
! saghas-saghs(anna)
! (a|e) is required for i->[] in cabh(a)ir 
! abhh(a)inn nneds fixing
! daing(e)an, ís(e)al
! but not cath^AOir 
! domhn (domhain)
! folmh (folamh) 
! bóthr(syncopated bóthar)
! ramh^Xr (ramhar)
! uasl (uasal) (^UA=ua diph.)
! máthr (máthair)
! dors (doras)
!***************************************************************************!

! cloigeann - cloigne, craiceann - craicne
.o.
[e a n n] -> [%^FY n e] || Cons  _ [Nountag+ |%^Adj] %^Coim		 !!≈ - @CODE@ 

.o.
! drioball - drible, fuigheall - fuighle
[e a l l] -> [%^FY l e],		 !!≈ - @CODE@ 
[a l l]   -> [%^FY l e] || Cons  _ [Nountag+ |%^Adj] %^Coim		 !!≈ - @CODE@ 

.o.
! abhaill - abhla, abhlacha
[a i l l]   -> [%^FY l a] || Cons  _ [Nountag+ |%^Adj] %^Coim		 !!≈ - @CODE@ 

.o.
[a|e|i|o|u] -> [%^FY] || Cons  (a|e) _  ?^<3  [Nountag+ |%^Adj] %^Coim		 !!≈ - @CODE@ 
.o.
 %^Coim -> []		 !!≈ - @CODE@ 
! ;
! 2004 include ^FY flag with consonants 
!***************************************************************************!
! (1) Insert "^X" before the final vowel group of every word in the lexicon
!***************************************************************************!
! 
! @-> = match longest string of vowels
! %^X... = insert ^X before the searched for text
! NOTE HFST does not implement the ... operator (unlike xfst and foma)
! changed to remove ... 19-12-2014 (EUD)
! use [..] to map a single empty string, not [] which can map an infinite number of empty strings 15-09-17 (EUD)
.o.
!!Vowel+ @-> %^X... || _ Cons* [Nountag|%^Adj]
[..] -> %^X || Cons _ Vowel+ Cons* [Nountag|%^Adj]

!***************************************************************************!
! (2) These markers are no longer required
!***************************************************************************!

.o.
 %^M -> []		 !!≈ - @CODE@ 
.o.
 %^F -> []		 !!≈ - @CODE@ 
.o.
 %^C -> []		 !!≈ - @CODE@ 
.o.
 %^G -> []		 !!≈ - @CODE@ 
.o.
 %^V -> []		 !!≈ - @CODE@ 
.o.
 %^Adj -> []		 !!≈ - @CODE@ 
! ;

! Athrú   : Ending Changes (^Ath)
!
! 2004 included ^FC in ea -> 0 correction rule 
!***************************************************************************!

.o.
 é -> %^FC é i || _ %^Ath	!!≈ - @CODE@  finné -> finnéi+the
!----------------------------------------------------------------------
.o.
 í -> %^FC i || _ %^Ath t	!!≈ - @CODE@  ainmhí -> ainmhi+the
				!!≈ - @CODE@  NOTE must fire before ch->í rule as we
				!!≈ - @CODE@  don't want the new í being turned into i
!----------------------------------------------------------------------
.o.
 i ú -> %^FC i || _ %^Ath t	!!≈ - @CODE@  deimhniú -> deimhni+the (pl)

.o.
 ú -> %^FC u i || _ %^Ath t	!!≈ - @CODE@  tarlú -> tarlui+the (pl)

!----------------------------------------------------------------------
.o.
 i ú -> [%^FC ] || _ %^Ath i	!!≈ - @CODE@  deimhniú -> deimhn+ithe (gen sg)

.o.
!!! ú -> [%^FC ] || _ %^Ath a	!!≈ - @CODE@  tarlú -> tarl+aithe (gen sg)
 ú -> [%^FC a] || _ %^Ath i	!!≈ - @CODE@  tarlú -> tarla+ithe (gen sg)

!----------------------------------------------------------------------
.o.
 a í -> [%^FC ] || _ %^Ath a	!!≈ - @CODE@  cónaí -> cón+aithe (gen sg)


!----------------------------------------------------------------------
! Nouns :final syllable "adh" become "tha|ta" , eg folcadh -> folctha
! moladh -> molta / moltaí
! and "eadh" become "the|te" loiceadh -> loicthe
! and "gt" become "gthe" tarraingt -> tarraingthe
!new 28/3/08

.o.
 m h n %^X a d h -> [%^FC m h a n]  || _ %^Ath (%^Caol) t ! tamhnadh -> tamhanta

.o.
 n l %^X a d h   -> [%^FC n n a l]  || _ %^Ath (%^Caol) t ! ionladh -> ionnalta

.o.
 a n n %^X a d h   -> [%^FC a n]  || _ %^Ath (%^Caol) t ! foirceannadh -> foirceanta

.o.
 n l %^X e a d h   -> [%^FC n n e a l]  || _ %^Ath (%^Caol) t ! inleadh -> innealta

.o.
 d l %^X a d h   -> [%^FC d l a]  || _ %^Ath (%^Caol) t	!!≈ - @CODE@  codladh -> codlata

.o.
 t h %^X a d h   -> [%^FC ]   || _ %^Ath (%^Caol) t	!!≈ - @CODE@  leathadh -> leata

.o.
 t h %^X a d h   -> [%^FC ]   || _ %^Ath (%^Caol) t	!!≈ - @CODE@  leathadh -> leata

.o.
 t e %^X a d h   -> [%^FC ]   || _ %^Ath (%^Caol) t		 !!≈ - @CODE@ 

.o.
 t %^X a d h     -> [%^FC ]   || _ %^Ath (%^Caol) t		 !!≈ - @CODE@ 

.o.
 e a d h     -> [%^FC ]   || %^X _ %^Ath (%^Caol) t		 !!≈ - @CODE@ 

.o.
    g t      -> [%^FC g]  || _ %^Ath (%^Caol) t	!!≈ - @CODE@  tarrXaingt -> tarrXaingthe

.o.
  a d h      -> [%^FC ]   || %^X _ %^Ath (%^Caol) t		 !!≈ - @CODE@ 

!.o.
!  a d h      -> [%^FC ]   ||  [d|n|t|l|s] %^X _ %^Ath (%^Caol) t
!! end new
 
!----------------------------------------------------------------------
!! Nouns :final syllable "ach"  and "adh" become "aí" , eg bealach 
!! "each" and "eadh" become "í"
!! stems must be polysyllabic e.g. not beach, sceach etc (teach is an exception)
!! Adj Like Nouns :final syllable "ach"  (no "adh"?) become "aí" , eg déanach  - déanaí

.o.
 c h -> %^FC í ,		!!≈ - @CODE@  bealach -> bealaí, soitheach -> soithí
				!!≈ - @CODE@  margadh -> margaí, geimhreadh -> geimhrí
 d h -> %^FC í || Vowel+ Cons+ %^X (e) a _ %^Ath (%^Caol) (\t)	!!≈ - @CODE@  added \t 31/08/06
!----------------------------------------------------------------------
.o.
 e a  -> [] || _ %^FC í %^Ath 	!!≈ - @CODE@  correction : geimhrea^FCí->geimhr^FCí, 
				!!≈ - @CODE@  soitheaí->soithí
				!!≈ - @CODE@  Adj: leochaileaí -> leochailí
!----------------------------------------------------------------------
.o.
 b h -> %^FC a í || _ %^Ath	!!≈ - @CODE@  leanbh -> leanaí

!----------------------------------------------------------------------
.o.
 d h -> %^FC t || i _ %^Ath %^Lea	!!≈ - @CODE@  iarraidh(aí) -> iarrait(aí)


!----------------------------------------------------------------------
.o.
 t -> [%^FC ] || n _ %^Ath	!!≈ - @CODE@  tiomáint -> tiomána  ( was t->a but D3 appenda a)

!----------------------------------------------------------------------
.o.
 t -> %^FC t h || r _ %^Ath	!!≈ - @CODE@  tagairt -> tagartha, buairt -> buartha

!----------------------------------------------------------------------
.o.
 e -> [%^FC ] || _ %^Ath	!!≈ - @CODE@  "e" is removed and "í" or "te" is appended
				!!≈ - @CODE@  buille -> buillí, císte -> cístí
				!!≈ - @CODE@  míle -> mílte, sloinne -> sloinnte
!----------------------------------------------------------------------
.o.
 i o -> %^FC e a || _ [c|n|s|r] (h t) %^Ath	!!≈ - @CODE@  sioc(m) -> seaca, 
					!!≈ - @CODE@  briocht -> breachta 
					!!≈ - @CODE@  smior(m) -> smeara
					!!≈ - @CODE@  fios(m) -> feasa, crios(m) -> creasa
					!!≈ - @CODE@  cion(m) -> ceana
					!!≈ - @CODE@  gion(m) -> geana
!
!----------------------------------------------------------------------
! Adjectives: final syllable íoch becomes íthí, e.g. buíoch -> buíthí
! AND [(e)á,ó,eo,ua] ch becomes thaí e.g.gnách -> gnáthaí, spleách->spleáthaí???

.o.
 o c h -> [%^FC ] || %^X (?) í _ %^Ath	!!≈ - @CODE@  buíoch -> buí +thí

.o.
 c h -> [%^FC ] || %^X [á|ó|o|%^UA] _ %^Ath	!!≈ - @CODE@  sách,sóch,gleoch,díomuach (^UA=ua)

! --- end adj stuff ---

!----------------------------------------------------------------------
.o.
%^Ath -> []
! ;

!
! (1) Remove the ^Caol (slenderise) tag if the word is already slender
!     i.e. if the last vowel is "i" or "í"
! (2) Remove the ^Lea (broaden) tag if the word is already broad
!     i.e. if the last vowel is NOT "i" or "í"
! (3) NB this must happen after Syncopation has taken place
!
! 2004 included %^FC and %^FY in Cons 
!***************************************************************************!

.o.
[ %^Caol -> [] || [i|í] Cons* _ 	!!≈ - @CODE@  search to check if 
				!!≈ - @CODE@  the last vowel is an "i" or "í" 
]

.o.
[									
 %^Lea -> [] || [a|o|u|á|ó|ú|%^UA|%^AO|%^AE|%^IA] Cons* _ 			 !!≈ - @CODE@ 
				!!≈ - @CODE@  check that the last vowel is NOT an "i" or "í" 
				!!≈ - @CODE@  U=ua O=ao E=ae (I=ia included though not in use)
]
! ;

! 2004 included %^FC and %^FY in Cons
!****************************************************************************
! ^Caol : Caolú (Attenuation) Slenderisation of broad words
!
! should be applied after Initial Mutation (after ^Urú, ^hv etc tags are removed)
!
!***************************************************************************


! marcach, clúdach, misneach, dúch
! polysyllabic stems only, e.g. not beach, laoch
.o.
 c h -> %^FC g h || Vowel+ Cons+ %^X (e) a _ %^Caol 		 !!≈ - @CODE@ 

.o.
! beithíoch -> beithígh (gs)		13/04/08
! also áinsíoch, gaiscíoch etc.
! polysyllabic stems only

c h -> %^FC g h || Vowel+ Cons+ %^X í o _ %^Caol 		 !!≈ - @CODE@ 

.o.
! exception dúch -> dúigh
c h -> %^FC g h || d ú _ %^Caol 		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! beach, sceach, nead, neamh (+e => all fem )
! also cuach, cruach, 
! but not misneach as ch has become gh (see top)
.o.
!!!!a -> %^FS i || %^X [e|u] _ [[c h] | d| m ] %^Caol e	
a -> %^FS i || %^X [e|u] _ [[c (h)] 	!!≈ - @CODE@ cuach - cuiche, 
                           |[b (h)]	!!≈ - @CODE@ dealbh - deilbhe
                           |[d (h)]	!!≈ - @CODE@ nead - neide, fleadh - fleidhe
                           |[l b h]	!!≈ - @CODE@ dealbh - deilbhe
                           |[n g]	!!≈ - @CODE@ reang - reinge
                           |[r c]	!!≈ - @CODE@ searc - seirce, leac - leice
                           |[r g]	!!≈ - @CODE@ fearg - feirge
                           |[t h]	!!≈ - @CODE@ leath - leithe
                           |[s]		!!≈ - @CODE@ greas - greise
                           |[m (h)]	!!≈ - @CODE@ neamh - neimhe (added '(h)') - CW 09/08
                           ] %^Caol e		 !!≈ - @CODE@ 
						
!-----------------------------------------------------------------------------

! fear, inneall, (all masc.?)
! leanbh???,geall , 
! airgead								
! beag -> bige adj
! not earc
.o.	
e a -> %^FS i || Cons+ %^X _ Cons+ %^Caol		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! éan, séipéal 
.o.
a -> %^FS i || %^X é _ Cons+ %^Caol		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! seol
! naomh, craobh, gaol, saol, fraoch
! ball, leabhar
! bruscar, bac but not balla
! bád, sleán, Seán 
! cnoc, post, tonn, bocht, 
! fód, glór
! punt, cúr, casúr, bruas
! oil(e)án, cin(e)ál, s(e)án
.o.
 [..] -> %^FS i || %^X (e) [%^AO|a|á|o|ó|u|ú|%^UA] _ Cons+ %^Caol 			 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! náis(i)ún, proifisiúin
.o.
 [..] -> %^FS i || %^X (i) ú _ Cons+ %^Caol 			 !!≈ - @CODE@ 
!-----------------------------------------------------------------------------
! íol, bíol, síol, buíon, suíomh
! fionn 
! eachaíos - eachaís, soilíos - soilís
.o.
 o -> [%^FS ] || %^X (a|u) [i|í] _ Cons+ %^Caol 		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! fliuch - fliche
.o.
 u -> [%^FS ] || %^X  [i|í] _ Cons+ %^Caol 		 !!≈ - @CODE@ 
!-----------------------------------------------------------------------------
! grian->gréine, pian->péine, 
! sliabh->sléibhte, 
! (h)iall->(h)éille
! liach - léiche
! cliabh needs fixing
.o.
 i a -> %^FS é i || %^X (h) _ Cons+ %^Caol (t) e		 !!≈ - @CODE@ 
!-----------------------------------------------------------------------------
! rian->riain
.o.
 [..] -> %^FS i || %^X i a _ Cons+ %^Caol .#.		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
! scian->sceana
.o.
i a -> %^FS e a || %^X _ Cons+ %^Caol a 		 !!≈ - @CODE@ 

!-----------------------------------------------------------------------------
.o.
 %^Caol -> []		 !!≈ - @CODE@ 
! ;

!! May 1998
!! Leathú (^Lea) :Broadening of slender words
!! Usually by removing "i"
!! but also by substituting vowels eg io->ea
!! and also by inserting a broad vowel eg í->ío
!!
!! included %^FH in i -> ea rule
!**************************************************************************

.o.
 o i  -> %^FB a || r _ Cons+ %^Lea	!!≈ - @CODE@  roinn -> rann 

.o.
 i -> %^FB a || [e|é] _ Cons+ %^Lea	!!≈ - @CODE@  greim(m) -> greama, spéir -> spéartha

.o. 
 i -> %^FB e a || [b|c|m|r] (%^FH h) %^X _ Cons+ %^Lea 			 !!≈ - @CODE@ 
				!!≈ - @CODE@  binn -> beanna, rith -> reatha
				!!≈ - @CODE@  cith(m) -> ceatha, crith(m) -> creatha
				!!≈ - @CODE@  scian -> sceana (I) imirt -> imeartha

 
.o.
 i -> [%^FB] || [a|á|%^AE|o|ó|ú|%^UA|%^AO] _ Cons+ %^Lea		 !!≈ - @CODE@ 
				!!≈ - @CODE@  CHECK WHETHER "u" SHOULD BE HERE TOO???
				!!≈ - @CODE@  OR DOES "ui" ALWAYS GO TO "o" (see next rule)
				!!≈ - @CODE@  it appears so ...
				!!≈ - @CODE@  súil - súl, dúil, glúin
				!!≈ - @CODE@  cóir - córa, móin - móna
				!!≈ - @CODE@  deoir - deora, feoil - feola
				!!≈ - @CODE@  troid - troda, cainteoir - cainteora
				!!≈ - @CODE@  droim(m) - droma, goin - gona, toil
				!!≈ - @CODE@  gáir - gártha, tiomáint - tiomána
				!!≈ - @CODE@  síocháin - síochána, cáisc - cásca
				!!≈ - @CODE@  stair - startha, flaith(m) - flatha
				!!≈ - @CODE@  bliain - bliana
				!!≈ - @CODE@  grósaeir - grósaera (E), traein - traen(E)
				!!≈ - @CODE@  bádóir - bádóra, buairt(U) -> buartha
				!!≈ - @CODE@  aoir - aortha (O)

.o.
 u i -> %^FB o || _ Cons+ %^Lea	!!≈ - @CODE@  cuid -  na coda, fuil - fola

.o.
 [..] -> %^FB o || (a) í _ Cons+ %^Lea 			 !!≈ - @CODE@ 
				!!≈ - @CODE@  feadaíl - feadaíola, tír - tíortha
.o.
%^Lea -> []		 !!≈ - @CODE@ 
! ;

!
!! Vowel Harmony of Broad and Slender Vowels: 
!!	Broad (back) - labio-velarised ; sounds like w-glide after vowel
!!	Slender (forward) - palatalised ; sounds like y-glide after vowel
!! Endings (which are appended to the root) must be broad or slender to agree with root 
!!
!! NB: this must be tested after ^Coim, ^Caol or ^Lea tags have been applied
!!
!! Examples 
!! - (1) (e)acha	: neadacha (nests), stoirmeacha (storms)
!! - (2) (e)anna	: carranna (cars),  áiteanna (places)
!!
!!  The broad ending "acha" or "anna" will be applied as standard. If the root
!!  is now found to be slender an "e" will be inserted
!
!***************************************************************************!
.o.
%^X -> []			!!≈ - @CODE@  no longer required and it would 
					!!≈ - @CODE@  get in the way of the next rule

.o.
! ^VH marker is replaced by "e" if final syllable is slender
[ %^VH -> e || [i|í] Cons* _ ]		!!≈ - @CODE@  check if the last vowel before the %^VH
					!!≈ - @CODE@  is slender (ie an "i" or "í" )
.o.
[ a -> [] || a %^VH _ ]		!!≈ - @CODE@  remove extre "a" in  Havana^VHach

.o.
 %^VH -> [] 		 !!≈ - @CODE@ 

.o.
[..] -> e || [e|é|i|í|%^AE] Cons* s _ %^Emph		 !!≈ - @CODE@ 

.o.
[..] -> a || [a|á|o|ó|u|ú|%^UA|%^IA|%^AO] Cons* s _ %^Emph		 !!≈ - @CODE@ 

.o.
[..] -> %- ||  s _ s (a|e) %^Emph		 !!≈ - @CODE@ 

.o.
%^Emph -> []		 !!≈ - @CODE@ 

! May 2001
! Last Part - tidy up

.o.
[ %^AE -> a e]		 !!≈ - @CODE@ 
.o.
![ %^IA -> i a]
!.o.
[ %^AO -> a o]		 !!≈ - @CODE@ 
.o.
[ %^UA -> u a]		 !!≈ - @CODE@ 
.o.
[%^F -> []]		 !!≈ - @CODE@ 
.o.
[%^M -> []]		 !!≈ - @CODE@ 
.o.
[%^N -> []]		 !!≈ - @CODE@ 
.o.
[%^G -> []]		 !!≈ - @CODE@ 
.o.
[%^V -> []]		 !!≈ - @CODE@ 
.o.
[%^CB -> []]		 !!≈ - @CODE@ 
 ;


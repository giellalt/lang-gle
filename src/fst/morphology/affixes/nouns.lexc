!**************************************************************************
!! # Moirfeolaíocht na nAinmfhocail Gaeilge (Morphology of Irish Nouns)
!**************************************************************************
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! +Idf is no longer used with base form .. just +DefArt after article ...
!**************************************************************************  
!
!!		C O N T I N U A T I O N     C L A S S E S
!
!**************************************************************************  
!!!LEXICON GuessNoun
!!!^GUESSNOUN	Nf2-SINGULAR;
!!!^GUESSNOUN	Nm1-1;
!!!^GUESSNOUN	Nf3-3;

!--------------------------------------------------------------------------    
!! ## FEMININE NOUN continuation classes
!--------------------------------------------------------------------------    
!! Weak Plurals : 
!! Broad singular is made slender; plural already broad

LEXICON Nf2-SINGULAR !!≈ - @CODE@ 

+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D2; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

LEXICON Nf2a-SINGULAR !!≈ - @CODE@ 

+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D2a; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

! not used
! beithir - beithre (gs) Sync + e
!LEXICON Nf2b-SINGULAR
!+Noun+Fem+Com:^F^C	Nom-sg; 
!+Noun+Fem+Gen:^F^G^Coim	Gen-sg-D2;
!+Noun+Fem+Voc:^F^V	Voc-sg-0;

LEXICON Nf2-1  !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Gen+Weak:^G	PL-TADA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(LEA)A; !!≈ - @CODE@ 

!GS tintreach - tintrí
LEXICON Nf2-1a  !!≈ - @CODE@ 
Nf2a-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Gen+Weak:^G	PL-TADA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(LEA)A; !!≈ - @CODE@ 

!--------------------------------------------------------------------------

!! ### Weak Plurals : Broaden 

!! #### Singular already slender; plural is made broad



LEXICON Nf2-2 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Gen+Weak:^G	PL-LEATHNÚ;
+Noun+Fem+Voc:^V		PL-(LEA)A;

!! ##### Weak Plurals : 

LEXICON Nf2-3 !!≈ - @CODE@ 

!! ##### Weak Plurals : 
Nf2-SINGULAR;

+Noun+Fem+Com:^C		PL-(CAOL)E; !!≈ - @CODE@ 
+Noun+Fem+Gen+Weak:^G	PL-LEATHNÚ; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(CAOL)E; !!≈ - @CODE@ 

LEXICON Nf2-4 !!≈ - @CODE@ 

Nf2a-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Gen+Weak:^G	PL-TADA;
+Noun+Fem+Voc:^V		PL-(LEA)A;

!! ### STRONG PLURALS


LEXICON Nf2-5 !!≈ - @CODE@ 

Nf2-SINGULAR; !!≈ - @CODE@ 

+Noun+Fem+Com:^C		PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-TE; !!≈ - @CODE@ 


! STRONG PLURALS

LEXICON Nf2-6 !!≈ - @CODE@ 

Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ANNA; !!≈ - @CODE@ 

! STRONG PLURALS

LEXICON Nf2-7 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-Í; !!≈ - @CODE@ 

! STRONG PLURALS

LEXICON Nf2-8 !!≈ - @CODE@ 
Nf2-SINGULAR;
+Noun+Fem+Com:^C		PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ACHA; !!≈ - @CODE@ 

! STRONG PLURALS

! not used
!LEXICON Nf2a-8
! Gen Sg = Sync + Slen + e
!Nf2b-SINGULAR;
!+Noun+Fem+Com:^C		PL-(E)ACHA;
!+Noun+Fem+Gen+Strong:^G		PL-(E)ACHA;
!+Noun+Fem+Voc:^V		PL-(E)ACHA;

! STRONG PLURALS

LEXICON Nf2-9 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)THA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(LEA)THA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(LEA)THA; !!≈ - @CODE@ 


LEXICON Nf2-10 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-TA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-TA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-TA; !!≈ - @CODE@ 

LEXICON Nf2-11 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ANTA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ANTA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ANTA; !!≈ - @CODE@ 


LEXICON Nf2-12 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(LEA)A; !!≈ - @CODE@ 

LEXICON Nf2-13 !!≈ - @CODE@ 
Nf2-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(CAOL)E; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G		PL-(CAOL)E; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(CAOL)E; !!≈ - @CODE@ 


!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
!! ### 3rd Declension
!
!! ### Strong Plurals : +aí

!! an bheannacht -> na beannachtaí


LEXICON Nf3-SINGULAR !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D3; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0; !!≈ - @CODE@ 

LEXICON Nf3a-SINGULAR !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C		Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G^Ath	Gen-sg-D3;	 !!≈ - @CODE@  EXCEPTION t-> th,  t->[], dh ->t
+Noun+Fem+Voc:^F^V		Voc-sg-0; !!≈ - @CODE@ 

!! gamhain - gamhna (gs), midheamhain - midheamhna (gs)
LEXICON Nf3b-SINGULAR !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C		Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G^Coim	Gen-sg-D3;	 !!≈ - @CODE@  SYNC + a 
+Noun+Fem+Voc:^F^V		Voc-sg-0; !!≈ - @CODE@ 


LEXICON 	Nf3-1 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-AÍ; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-AÍ; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-AÍ; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
LEXICON 	Nf3-2 !!≈ - @CODE@ 
Nf3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Ath^Lea		PL-AÍ; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Ath^Lea	PL-AÍ; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Ath^Lea		PL-AÍ; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
!! Strong Plurals :  +(e)anna

!! tóin -> tóineanna
!! scoth -> scothanna
!! EXCEPTION: an chuid -> na codanna see FIX file
!! EXCEPTION: an raith -> na rathanna see FIX file
!
LEXICON 	Nf3-3 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ANNA; !!≈ - @CODE@ 
!--------------------------------------------------------------------------

LEXICON 	Nf3-4	 !!≈ - @CODE@ 
Nf3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-Í; !!≈ - @CODE@ 

!--------------------------------------------------------------------------

LEXICON 	Nf3-5 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Lea		PL-TA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Lea	PL-TA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Lea		PL-TA; !!≈ - @CODE@ 


!--------------------------------------------------------------------------

LEXICON 	Nf3-6 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G		PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ACHA; !!≈ - @CODE@ 

!--------------------------------------------------------------------------

LEXICON 	Nf3-6a !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(LEA)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G		PL-(LEA)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(LEA)ACHA; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
LEXICON 	Nf3-7 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G		PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-TE; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
!! Strong Plurals :  Broaden +anna

!! an chuid -> na codanna see FIX file
!! an raith -> na rathanna
!! an laith -> na lathanna
!! an luaith -> na luathanna
!
LEXICON 	Nf3-8 !!≈ - @CODE@ 
Nf3-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Lea		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Lea		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Lea		PL-(E)ANNA; !!≈ - @CODE@ 
!--------------------------------------------------------------------------


LEXICON 	Nf4-SINGULAR !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D4; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0; !!≈ - @CODE@ 


!! Strong Plurals : +í

!! an bhearna -> na bearnaí
!! an eala -> na healaí

LEXICON 	Nf4-1 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-Í; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
!! Strong Plurals : Athrú e -> í

!! an aicme -> na haicmí (classes)
!! an táille -> na táillí (fees)

LEXICON 	Nf4-2 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Ath		PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Ath	PL-Í; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Ath		PL-Í; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
!! Strong Plurals : 


LEXICON 	Nf4-3 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Ath		PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Ath	PL-TE; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Ath		PL-TE; !!≈ - @CODE@ 

LEXICON 	Nf4-4 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Ath		PL-THE; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Ath	PL-THE; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Ath		PL-THE; !!≈ - @CODE@ 

LEXICON 	Nf4-5 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ACHA;
+Noun+Fem+Voc:^V		PL-(E)ACHA;


LEXICON 	Nf4-6 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-(E)ANNA; !!≈ - @CODE@ 

LEXICON 	Nf4-7 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-(E)ANTA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-(E)ANTA;
+Noun+Fem+Voc:^V		PL-(E)ANTA;
!!
!! various ending in vowel	! plurals +nna
LEXICON 	Nf4-8 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-NNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-NNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-NNA; !!≈ - @CODE@ 
!!
LEXICON 	Nf4-9 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-ONNA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-ONNA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-ONNA; !!≈ - @CODE@ 
!!
LEXICON 	Nf4-10 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C		PL-OCHA; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G	PL-OCHA; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V		PL-OCHA; !!≈ - @CODE@ 
!!
LEXICON 	Nf4-11 !!≈ - @CODE@ 
Nf4-SINGULAR; !!≈ - @CODE@ 
+Noun+Fem+Com:^C^Ath		PL-TÍ; !!≈ - @CODE@ 
+Noun+Fem+Gen+Strong:^G^Ath	PL-TÍ; !!≈ - @CODE@ 
+Noun+Fem+Voc:^V^Ath		PL-TÍ; !!≈ - @CODE@ 

!--------------------------------------------------------------------------
!! Strong Plurals : Leathnú  +acha

!! an bheoir -> na beoracha (beers)

LEXICON 	Nf5-1 !!≈ - @CODE@ 

+Noun+Fem+Com:^F^C	Nom-sg; 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-1;
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Lea		PL-(E)ACHA;
+Noun+Fem+Gen+Strong:^G^Lea	PL-(E)ACHA;
+Noun+Fem+Voc:^V^Lea		PL-(E)ACHA;


!--------------------------------------------------------------------------
!! Gen Sg : Coim + ach
!! Strong Plurals : Coimriú +eacha

!! an chathaoir -> na cathaoireacha (chairs) (Note long vowel aoi is not sync.
!! an cathair -> na cathracha

LEXICON 		Nf5-2  !!≈ - @CODE@ 

+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-2; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Coim			PL-(E)ACHA;
+Noun+Fem+Gen+Strong:^G^Coim	PL-(E)ACHA;
+Noun+Fem+Voc:^V^Coim			PL-(E)ACHA;

!--------------------------------------------------------------------------
!! Gen Sg : Coim + a
!! Strong Plurals : Coimriú +(e)acha
!! samhail -> samhla
!! anacair -> anacra
LEXICON 		Nf5-2a !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-2a; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Coim			PL-(E)ACHA;
+Noun+Fem+Gen+Strong:^G^Coim	PL-(E)ACHA;
+Noun+Fem+Voc:^V^Coim			PL-(E)ACHA;
!--------------------------------------------------------------------------
!! Gen Sg : Coim + Slen + e
!! Strong Plurals : Coimriú +(e)acha
!! crithir - critre
!! fothair - foithre
LEXICON 		Nf5-2b !!≈ - @CODE@ 

+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-2b; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Coim			PL-(E)ACHA;
+Noun+Fem+Gen+Strong:^G^Coim	PL-(E)ACHA;
+Noun+Fem+Voc:^V^Coim			PL-(E)ACHA;
!--------------------------------------------------------------------------

LEXICON 		Nf5-3 !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-3;
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C		PL-NA;
+Noun+Fem+Gen+Strong:^G	PL-NA;
+Noun+Fem+Voc:^V		PL-NA;

LEXICON 		Nf5-4 !!≈ - @CODE@ 
+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-5; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Coim			PL-(CAOL)EACHA;
+Noun+Fem+Gen+Strong:^G^Coim	PL-(CAOL)EACHA;
+Noun+Fem+Voc:^V^Coim			PL-(CAOL)EACHA;

!! tarraingt - tarraingthe - tarraingtí
LEXICON 		Nf5-7		 !!≈ - @CODE@  11/04/08

+Noun+Fem+Com:^F^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Fem+Gen:^F^G	Gen-sg-D5-7; !!≈ - @CODE@ 
+Noun+Fem+Voc:^F^V	Voc-sg-0;

+Noun+Fem+Com:^C^Coim		PL-Í;
+Noun+Fem+Gen+Strong:^G^Coim	PL-Í;
+Noun+Fem+Voc:^V^Coim		PL-Í;


!************************************************************************** 
!--------------------------------------------------------------------------    
!! ## MASCULINE NOUN continuation classes
!--------------------------------------------------------------------------    



LEXICON Nm1-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D1; !!≈ - @CODE@ 
+Noun+Masc+Voc:^M^V	Voc-sg-1; !!≈ - @CODE@ 


!! WEAK PLURALS (i.e. where the nominative and genitive plurals are different) 
!
!! TYPE 1 Nom pl. ends in conson. eg cat : cait, fear : fir, marcach: marcaigh 

LEXICON Nm1-1 !!≈ - @CODE@ 

Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-CAOLÚ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Weak:^G	PL-TADA; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V		PL-(LEA)A; !!≈ - @CODE@ 


!---------------------------------------------------------------------------
!! TYPE 2 Nom pl. formed by adding -a eg cos : cosa, úll : úlla 

LEXICON Nm1-2  !!≈ - @CODE@ 

Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Masc+Gen+Weak:^G	PL-TADA; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V		PL-(LEA)A;

!---------------------------------------------------------------------------
!! (TYPE 3) Nom pl. formed by adding -ta eg 

LEXICON Nm1-3  !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-TA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-TA; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V			PL-TA;

LEXICON Nm1-4 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(LEA)THA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(LEA)THA; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V			PL-(LEA)THA;

LEXICON Nm1-5 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-ATHRÚ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-ATHRÚ; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V			PL-ATHRÚ;

LEXICON Nm1-6 !!≈ - @CODE@ 
Nm1-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Voc:^V			PL-(E)ANNA;
!!
LEXICON Nm1-7 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Caol			PL-Í; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Caol	PL-Í;
+Noun+Masc+Voc:^V^Caol			PL-Í;
!!

LEXICON Nm1-8 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Coim			PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Coim	PL-(E)ANNA;
+Noun+Masc+Voc:^V^Coim			PL-(E)ANNA;

LEXICON Nm1-9 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Coim			PL-(CAOL)E; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Coim	PL-(CAOL)E;
+Noun+Masc+Voc:^V^Coim			PL-(CAOL)E;

LEXICON Nm1-10 !!≈ - @CODE@ 
Nm1-SINGULAR;  !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Coim			PL-(CAOL)EACHA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Coim	PL-(CAOL)EACHA;
+Noun+Masc+Voc:^V^Coim			PL-(CAOL)EACHA;

LEXICON Nm1-11 !!≈ - @CODE@ 
Nm1-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ACHA;
+Noun+Masc+Voc:^V			PL-(E)ACHA;
!---------------------------------------------------------------------------
!! ## 2nd Declension
!! sliabh -> na sléibhte 

LEXICON Nm2-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D2;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

LEXICON Nm2-1 !!≈ - @CODE@ 
Nm2-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Caol			PL-TE; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Caol	PL-TE;
+Noun+Masc+Voc:^V^Caol			PL-TE;

!---------------------------------------------------------------------------

LEXICON Nm3-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D3;
+Noun+Masc+Voc:^M^V	Voc-sg-0;
!!
LEXICON Nm3a-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G^Ath	Gen-sg-D3;
+Noun+Masc+Voc:^M^V	Voc-sg-0;
!!
!! 3rd Declension
!! Strong Plurals : +í 
!!     as in Nm7 but singular are different

!! eg. bádóir -> na bádóirí

LEXICON Nm3-1  !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-Í; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-Í;
+Noun+Masc+Voc:^V			PL-Í;

!---------------------------------------------------------------------------
!! Strong Plurals : +anna

!! eg. an bláth -> na bláthanna

LEXICON Nm3-2  !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ANNA;
+Noun+Masc+Voc:^V			PL-(E)ANNA;
!---------------------------------------------------------------------------
!! Strong Plurals : +aí

LEXICON Nm3-3 !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-AÍ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G		PL-AÍ;
+Noun+Masc+Voc:^V			PL-AÍ;

!---------------------------------------------------------------------------
!! gen briocht -> breachta
!! Strong Plurals : +aí
!! briocht -> briochtaí

LEXICON Nm3-3a !!≈ - @CODE@ 
Nm3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-AÍ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G		PL-AÍ;
+Noun+Masc+Voc:^V			PL-AÍ;

!---------------------------------------------------------------------------
!! Strong Plurals : +anna
!! eg. an bláth -> na bláthanna

LEXICON Nm3-4a !!≈ - @CODE@ 

!! ^Lea,broadening, is required, in gen sg: io -> ea (bior, crios) 
!! and this is done using ^Ath (change)
!! pl bior -> bioranna

Nm3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ANNA;
+Noun+Masc+Voc:^V			PL-(E)ANNA;

!---------------------------------------------------------------------------
!! ### Strong Plurals : Athrú +anna (io->ea)
!! eg. an cith -> na ceathanna
LEXICON Nm3-4 !!≈ - @CODE@ 
Nm3a-SINGULAR; !!≈ - @CODE@ 
!! ^Lea,broadening, is required, in gen sg: cith -> ceatha, greim -> greama
!! and this is done using ^Ath (change)
!! pl also broadened cith -> ceathanna
+Noun+Masc+Com:^C^Ath^Lea		PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Ath^Lea	PL-(E)ANNA;	
+Noun+Masc+Voc:^V^Ath^Lea		PL-(E)ANNA;		

LEXICON Nm3-5 !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ACHA;
+Noun+Masc+Voc:^V			PL-(E)ACHA;

LEXICON Nm3-6 !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(LEA)A;
+Noun+Masc+Voc:^V			PL-(LEA)A;

LEXICON Nm3-7 !!≈ - @CODE@ 
Nm3-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-TA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-TA;
+Noun+Masc+Voc:^V			PL-TA;

LEXICON Nm3-8 !!≈ - @CODE@ 
Nm3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Masc+Gen+Weak:^G	PL-TADA;
+Noun+Masc+Voc:^V		PL-(LEA)A;

LEXICON Nm3-8a !!≈ - @CODE@  sliocht - sleachta gs & pl
Nm3a-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Ath		PL-(LEA)A; !!≈ - @CODE@ 
+Noun+Masc+Gen+Weak:^G^Ath		PL-(LEA)A;
+Noun+Masc+Voc:^V^Ath		PL-(LEA)A;


!---------------------------------------------------------------------------
!---------------------------------------------------------------------------
!! Strong Plurals : +í

!! (A) nouns ending in -ín (a diminutive)
!! smidiríní (smithereens) no singular
!! eg. an cailín -> na cailíní (girls)
!! eg. an báidín -> na báidíní (small boats)

!! (B) nouns ending in -a
!! eg. an balla -> na ballaí (walls)


LEXICON Nm4-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D4;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

LEXICON Nm4-1 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-Í; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-Í;
+Noun+Masc+Voc:^V			PL-Í;


LEXICON Nm4-2 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Ath			PL-Í; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Ath	PL-Í;
+Noun+Masc+Voc:^V^Ath			PL-Í;


LEXICON Nm4-3 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Ath			PL-TE; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Ath	PL-TE;
+Noun+Masc+Voc:^V^Ath			PL-TE;

LEXICON Nm4-4 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Ath			PL-THE; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Ath	PL-THE;
+Noun+Masc+Voc:^V^Ath			PL-THE;

LEXICON Nm4-5 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ACHA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ACHA;
+Noun+Masc+Voc:^V			PL-(E)ACHA;

LEXICON Nm4-6 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-NNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-NNA;
+Noun+Masc+Voc:^V			PL-NNA;

LEXICON Nm4-7 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-(E)ANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(E)ANNA;
+Noun+Masc+Voc:^V			PL-(E)ANNA;

LEXICON Nm4-8 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-ITE; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-ITE;
+Noun+Masc+Voc:^V			PL-ITE;

LEXICON Nm4-9 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C			PL-ONNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-ONNA;
+Noun+Masc+Voc:^V			PL-ONNA;

LEXICON Nm4-10 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-OCHA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-OCHA;
+Noun+Masc+Voc:^V		PL-OCHA;

LEXICON Nm4-11 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-(LEA)THA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-(LEA)THA;
+Noun+Masc+Voc:^V		PL-(LEA)THA;

!! 01/04/08
LEXICON Nm4-12 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-AÍ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-AÍ;
+Noun+Masc+Voc:^V		PL-AÍ;

LEXICON Nm4-13 !!≈ - @CODE@ 
Nm4-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C		PL-THANNA; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-THANNA;
+Noun+Masc+Voc:^V		PL-THANNA;

!---------------------------------------------------------------------------
!---------------------------------------------------------------------------
!! Strong Plurals : +idí
!!!!!! SINGULAR  ALSO IN NUM-LEX.TXT
!! an fiche -> na fichidí (the twenties) eidí needs correcting
!! an caoga -> na caogaidí (the fifties)
LEXICON Nm5-1 !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg; !!≈ - @CODE@  
+Noun+Masc+Gen:^M^G	Gen-sg-D5-4;
!!!+Noun+Masc+Voc:^M^V	Voc-sg-0;
+Noun+Masc+Com:^C			PL-IDÍ; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G	PL-IDÍ;
+Noun+Masc+Voc:^V			PL-IDÍ;


LEXICON Nm5-2 !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg; !!≈ - @CODE@  
+Noun+Masc+Gen:^M^G	Gen-sg-D5-5;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

+Noun+Masc+Com:^C^Coim			PL-(CAOL)EACHA;
+Noun+Masc+Gen+Strong:^G^Coim	PL-(CAOL)EACHA;
+Noun+Masc+Voc:^V^Coim			PL-(CAOL)EACHA;

LEXICON Nm5-3 !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg; !!≈ - @CODE@  
+Noun+Masc+Gen:^M^G	Gen-sg-D5-6;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

+Noun+Masc+Com:^C^Ath			PL-THE;
+Noun+Masc+Gen+Strong:^G^Ath	PL-THE;
+Noun+Masc+Voc:^V^Ath			PL-THE;

!! GS +the
LEXICON Nm5-4 !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D5-7;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

!!!PL?????????

!! GS +te
LEXICON Nm5-5-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D5-8;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

LEXICON Nm5-5				 !!≈ - @CODE@  PL +TÍ 
Nm5-5-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Atht			PL-Í; !!≈ - @CODE@ 
+Noun+Masc+Gen+Strong:^G^Atht		PL-Í;
+Noun+Masc+Voc:^V^Atht			PL-Í;


!! GS +tha
!! PL +thaí
!! bascadh - basctha - bascthaí
LEXICON Nm5-6 !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg; 
+Noun+Masc+Gen:^M^G	Gen-sg-D5-9;
+Noun+Masc+Voc:^M^V	Voc-sg-0;
+Noun+Masc+Com:^C^Aththa		PL-Í;
+Noun+Masc+Gen+Strong:^G^Aththa		PL-Í;
+Noun+Masc+Voc:^V^Aththa		PL-Í;

!!!PL?????????

!! GS +ta
LEXICON Nm5-7-SINGULAR !!≈ - @CODE@ 
+Noun+Masc+Com:^M^C	Nom-sg;  !!≈ - @CODE@ 
+Noun+Masc+Gen:^M^G	Gen-sg-D5-10;
+Noun+Masc+Voc:^M^V	Voc-sg-0;

LEXICON Nm5-7 !!≈ - @CODE@ 
Nm5-7-SINGULAR; !!≈ - @CODE@ 

!! moladh / gs = molta / pl = moltaí
!!
LEXICON Nm5-7a !!≈ - @CODE@ 
Nm5-7-SINGULAR; !!≈ - @CODE@ 
+Noun+Masc+Com:^C^Atht			PL-AÍ;
+Noun+Masc+Gen+Strong:^G^Atht		PL-AÍ;
+Noun+Masc+Voc:^V^Atht			PL-AÍ;

!----------------------------------------------------------------------------
LEXICON Ns !!≈ - @CODE@ 
+Subst+Noun+Sg:0	NounSéUrú; !!≈ - @CODE@ 

LEXICON Nspl !!≈ - @CODE@ 
+Subst+Noun+Pl:0	NounSéUrú; !!≈ - @CODE@ 

!************************************************************************** 
!! ## INITIAL MUTATIONS
!************************************************************************** 
!-------------------
!! ### NOMINATIVE SINGULAR 
!-------------------
LEXICON Nom-sg-initial !!≈ - @CODE@ 
NounSéUrú;		 !!≈ - @CODE@ 
+DefArt:^Sé^tv^ts	#; 	!! definite article eg. an cat (m) (p38 NIG)
			!! an t-éan(m) - the bird (p39 NIG) 
			!! an bhróg(f) - the shoe
			!! initial mutation for other reasons
			!! e.g. comp. preps. ar an gcnoc, poss. mo chat

!! ^IM = initial mutation e.g. with prepositions, and possession
!! Singular:
!! e.g. ar an bhosca, ar an mbosca
!! possessive markers on vowels: ár n-athair, a (f) hathair, 
!! Plural:
!! e.g. ar bhoscaí, i mboscaí
!! possessive e.g. ár n-aithreacha - our fathers (^C)

LEXICON NounSéUrú !!≈ - @CODE@ 
#;			!" indefinite => no initial mutation
Emphasis; !!≈ - @CODE@ 
+Ecl:^IM^Urú	Emphasis;		 !!≈ - @CODE@ 
+Len:^IM^Sé	Emphasis;		
+hPref:^IM^hv	Emphasis;	!! adds ^h to vowel-initial words ...  but adds the +hPref to all words ... see fix file
					

LEXICON Emphasis !!≈ - @CODE@ 
#; !!≈ - @CODE@ 
+Emph:s^Emph	#;   			 !!≈ - @CODE@   NEEDS 1PL NE/NA & 3PL SEAN/SAN ALSO
!!! added 8/5/2014 EUD for theach-sa etc.
+Emph:%-s^Emph	#;   			!  NEEDS 1PL NE/NA & 3PL SEAN/SAN ALSO	


!-------------------
!! ### GENITIVE SINGULAR 
!-------------------

LEXICON Gen-sg-initial !!≈ - @CODE@ 
NounSéUrú;			! eg. tábhacht a n-oidhreachta
#; 			! eg. bia cait, bia sagairt, bia stóir, 
			!     bia rúin, pobail  
			!     siopa grósaera - a grocer's shop
+DefArt:^Sé^hv^ts	#; 	! eg. bia an chait,an íl, bia an stóir 
			!     bia an éin (the bird's food)
			! eg. bia an tsagairt (overgenerates 
			!     tshagairt, h will be removed,
			! eg. bia an rúin, an phobail
			! eg. siopa an ghrósaera - the grocer's shop
!-------------------
!! ### VOCATIVE SINGULAR 
!-------------------
!! Since this is trivial (always ^Sé) it is included with Final Mutations
!! in Voc-sg-0 and Voc-sg-1.
					
!------------
!! ### ALL PLURALS
!------------
!! Note: Vocative Plural does not require Def & Idf but it is easier to generate 
!! them and remove all Voc Pl Idfs at the end (the Def form is correct 
!! although the Def marker is unnecessary)

LEXICON Pl-initial !!≈ - @CODE@ 
+DefArt:^Sé^Urú^hv			#; 	! eg. na cait (m)
NounSéUrú;					! optional initial mutations for posession, after prepositions

!************************************************************************** 
!! ## FINAL MUTATIONS
!************************************************************************** 
!-------------------
!! ### NOMINATIVE SINGULAR
!-------------------
LEXICON Nom-sg !!≈ - @CODE@ 
+Sg:0	Nom-sg-initial; !!≈ - @CODE@ 

!-------------------
!! ### GENITIVE SINGULAR 
!-------------------

LEXICON Gen-sg-D1		 !!≈ - @CODE@  1st. Declension - Caolú (Slenderise)
+Sg:^Caol	Gen-sg-initial; !!≈ - @CODE@ 

LEXICON Gen-sg-D2		 !!≈ - @CODE@  2nd. Declension - Caolú (Slenderise) and
				! Add "e"
+Sg:^Caole	Gen-sg-initial;

LEXICON Gen-sg-D2a		 !!≈ - @CODE@  2nd. Declension - each -> í, ach -> aí 
				! tintreach - tintrí 
+Sg:^Ath	Gen-sg-initial;

LEXICON Gen-sg-D3		 !!≈ - @CODE@  3rd. Declension -  Broaden (Leathnú) and
				! Add "a"
+Sg:^Leaa	Gen-sg-initial;


LEXICON Gen-sg-D4		 !!≈ - @CODE@  4th. Declension - No change
+Sg:0	Gen-sg-initial;

LEXICON Gen-sg-D5-1		 !!≈ - @CODE@  5th. Declension - (Broaden) + ach
+Sg:^Leaach	Gen-sg-initial;
LEXICON Gen-sg-D5-2		 !!≈ - @CODE@  5th. Declension - Sync + (e)ach
+Sg:^Coim^VHach	Gen-sg-initial;
LEXICON Gen-sg-D5-2a		 !!≈ - @CODE@  5th. Declension - Sync + a
+Sg:^Coim^VHa	Gen-sg-initial;
LEXICON Gen-sg-D5-2b		 !!≈ - @CODE@  5th. Declension - Sync + Slen + e
+Sg:^Coim^Caole	Gen-sg-initial;
LEXICON Gen-sg-D5-3		 !!≈ - @CODE@  5th. Declension - Add "n"
+Sg:n		Gen-sg-initial;
LEXICON Gen-sg-D5-4		 !!≈ - @CODE@  5th. Declension - Add "d"
+Sg:d		Gen-sg-initial;
LEXICON Gen-sg-D5-5		 !!≈ - @CODE@  5th. Declension -  Broaden (Leathnú)
+Sg:^Lea	Gen-sg-initial;
LEXICON Gen-sg-D5-6		 !!≈ - @CODE@  5th. Declension -  Change + ithe
+Sg:^Athithe	Gen-sg-initial;
LEXICON Gen-sg-D5-7		 !!≈ - @CODE@  5th. Declension -  Change + Slen + the
+Sg:^Ath^Caolthe	Gen-sg-initial;
LEXICON Gen-sg-D5-8		 !!≈ - @CODE@  5th. Declension -  Change + Slen + te
+Sg:^Ath^Caolte	Gen-sg-initial;
LEXICON Gen-sg-D5-9		! 5th. Declension -  Change + tha
+Sg:^Aththa	Gen-sg-initial;
LEXICON Gen-sg-D5-10		! 5th. Declension -  Change + ta
+Sg:^Athta	Gen-sg-initial;

!-------------------
!! ### VOCATIVE SINGULAR 
!-------------------

LEXICON Voc-sg-0 !!≈ - @CODE@ 
+Sg+Len:^Sé		#;	 !!≈ - @CODE@  eg. a rún, a phobal

LEXICON Voc-sg-1 !!≈ - @CODE@ 
+Sg+Len:^Caol^Sé	#; 	! eg. a éin - O bird!
				!     a chait, a íl 
				! eg. a shagairt 

LEXICON Voc-initial
+Len:^IM^Sé		#;	! Jul 2012 added +Len

!-------------------------------------------------------------------------
!-------------------
!! ### ALL PLURALS
!-------------------


LEXICON PL-TADA					 !!≈ - @CODE@  NO CHANGE (LIT. NOTHING)
+Pl:0		Pl-initial;


LEXICON PL-CAOLÚ				 !!≈ - @CODE@  CAOLÚ (SLENDERISE)
+Pl:^Caol	Pl-initial;


LEXICON PL-LEATHNÚ				 !!≈ - @CODE@  LEATHNÚ (BROADEN)
+Pl:^Lea	Pl-initial;


LEXICON PL-(LEA)A				 !!≈ - @CODE@  ADD "A" & LEATHNÚ (BROADEN) IF SLENDER
+Pl:^Leaa	Pl-initial;


LEXICON PL-TA					 !!≈ - @CODE@  ADD "TA"
+Pl:ta		Pl-initial;


LEXICON PL-(LEA)THA				 !!≈ - @CODE@  ADD "THA" & LEATHNÚ IF SLENDER
+Pl:^Leatha	Pl-initial;


LEXICON PL-(E)ANNA				 !!≈ - @CODE@  ADD "ANNA" IF BROAD & ADD "EANNA" IF SLENDER
+Pl:^VHanna	Pl-initial;


LEXICON PL-Í					 !!≈ - @CODE@  ADD "Í"
+Pl:í		Pl-initial;

LEXICON PL-TÍ					 !!≈ - @CODE@  ADD "TÍ"
+Pl:tí		Pl-initial;


LEXICON PL-(CAOL)E				 !!≈ - @CODE@  ADD "E" & CAOLÚ IF BROAD
+Pl:^Caole	Pl-initial;


LEXICON PL-(CAOL)EACHA			!!≈ - @CODE@  ADD "EACHA" & CAOLÚ
+Pl:^Caoleacha	Pl-initial;

LEXICON PL-(LEA)ACHA			!!≈ - @CODE@  ADD "ACHA" & LEATHNÚ
+Pl:^Leaacha	Pl-initial;


LEXICON PL-(E)ACHA				!!≈ - @CODE@  ADD "ACHA" IF BROAD & ADD "EACHA" IF SLENDER
+Pl:^VHacha	Pl-initial;


LEXICON PL-THANNA				!!≈ - @CODE@  ADD "THANNA"
+Pl:thanna	Pl-initial;

LEXICON PL-TE					!!≈ - @CODE@  ADD "TE"
+Pl:te		Pl-initial;


LEXICON PL-AÍ					!!≈ - @CODE@  ADD "AÍ"
+Pl:aí		Pl-initial;


LEXICON PL-NNA					!!≈ - @CODE@  ADD "NNA"
+Pl:nna		Pl-initial;


LEXICON PL-THE					!!≈ - @CODE@  ADD "THE"
+Pl:the		Pl-initial;


LEXICON PL-NA					!!≈ - @CODE@  ADD "NA"
+Pl:na		Pl-initial;


LEXICON PL-IDÍ					!!≈ - @CODE@  ADD "IDÍ"
+Pl:idí		Pl-initial;


LEXICON PL-ATHRÚ				!!≈ - @CODE@  ATHRÚ (CHANGE LAST SYLLABLE)
+Pl:^Ath	Pl-initial;

LEXICON PL-(E)ANTA				!!≈ - @CODE@  ADD "ANTA" IF BROAD & ADD "EANTA" IF SLENDER
+Pl:^VHanta	Pl-initial;

LEXICON PL-ITE					!!≈ - @CODE@  ADD "ITE"
+Pl:ite		Pl-initial;

LEXICON PL-ONNA					!!≈ - @CODE@  ADD "ONNA"
+Pl:onna		Pl-initial;

LEXICON PL-OCHA					!!≈ - @CODE@  ADD "OCHA"
+Pl:ocha		Pl-initial;

!---------------------------------------------------------------------------

!**************************************************************************
! ## Moirfeolaíocht na nAinmfhocail Gaeilge (Morphology of Irish Nouns)
!**************************************************************************
!!	###	PROPER NOUNS
!**************************************************************************
!! E. Uí Dhonnchadha 
!  
! April 2003: ^IM general initial mutation flag
!
!! ADDED THE +SG TAG 20/2/2004
!! ADDED NP-Fam Aug 2004
!**************************************************************************  
!**************************************************************************  
!!				CONTINUATION CLASSES
!**************************************************************************  


LEXICON NP-PLACE !!≈ - @CODE@ 
+Place+Prop+Noun+Masc:	NpSéUrú;

! not necessary?
!LEXICON NP-PLACE-EN-fem
!+Place+Prop+Noun+Fem:		NpSéUrú-EN;

LEXICON NP-PLACE-EN !!≈ - @CODE@ 
+Place+Prop+Noun+Masc:	NpSéUrú-EN;

LEXICON NP-PLACE-sg !!≈ - @CODE@ 
+Place+Prop+Noun+Masc:	NpSéUrú;
+Adj+Base+DeNom:^VHach	NpSéUrú;

LEXICON NP-PLACE-fem !!≈ - @CODE@ 
+Place+Prop+Noun+Fem:		NpSéUrú-fem;
+Adj+Base+DeNom:^VHach	NpSéUrú;

LEXICON NP-PLACE-pl !!≈ - @CODE@ 
+Place+Prop+Noun+Masc:	NpSéUrú-PL;

LEXICON NP-FAM-EN !!≈ - @CODE@ 
!+Fam+Prop+Noun+Masc:	NpSéUrú;
+Fam+Prop+Noun+Masc:	NpSéUrú-EN;

LEXICON NP-PERS-EN-fem !!≈ - @CODE@ 
!+PersName+Prop+Noun+Fem:	NpSéUrú;
+PersName+Prop+Noun+Fem:	NpSéUrú-EN;

LEXICON NP-PERS-EN !!≈ - @CODE@ 
!+PersName+Prop+Noun+Masc:	NpSéUrú;
+PersName+Prop+Noun+Masc:	NpSéUrú-EN;


LEXICON NP-Fam !!≈ - @CODE@ 
+Fam+Prop+Noun+Masc+Com+Sg+hPref:^IM^hv		#;
+Fam+Prop+Noun+Masc+Gen+Sg:^M^G^Caol^hv^ts		#; ! ^Caol not yet tested
+Fam+Prop+Noun+Fem+Gen+Sg+Len:^IM^Sé		#;
!+Prop+Noun+Masc+Gen+Sg:^M^G^Sé		#;
+Fam+Prop+Noun+Masc+Com+Sg+DefArt:^M^C^Lea^tv^VHach		#;

LEXICON NP-Fam-0 !!≈ - @CODE@ 
+Fam+Prop+Noun+Masc+Com+Sg+hPref:^IM^hv		#;
+Fam+Prop+Noun+Masc+Gen+Sg:^M^G^hv^ts		#;
+Fam+Prop+Noun+Fem+Gen+Sg+Len:^IM^Sé		#;
+Fam+Prop+Noun+Masc+Com+Sg+DefArt:^IM^tv		#;

LEXICON NP-Fam-V !!≈ - @CODE@ 
+Fam+Prop+Noun+Masc+Com+Sg+hPref:^IM^hv		#;
+Fam+Prop+Noun+Masc+Gen+Sg:^M^G^hv^ts		#;
+Fam+Prop+Noun+Fem+Gen+Sg+Len:^IM^Sé		#;
+Fam+Prop+Noun+Masc+Com+Sg+DefArt:^IM^tvch	#;

LEXICON NP-Fam-chg !!≈ - @CODE@ 
+Fam+Prop+Noun+Masc+Com+Sg+hPref:^IM^hv		#;
+Fam+Prop+Noun+Masc+Gen+Sg:^M^G^hv^ts		#;
+Fam+Prop+Noun+Fem+Gen+Sg+Len:^IM^Sé		#;
+Fam+Prop+Noun+Masc+Com+Sg+DefArt:^Ath^IM^tv	#;

!! when it is a place name
!! as well as the usual inflections for propernouns  (4 classes)
!! we want to generate an adjectival form e.g. Beilg - Beilgeach


!! new 5-6-2024
!! Place and Personal name files both use Nf1-Prop and Nm1-Prop etc.
!
! currently not used
!LEXICON Nf1-Prop-Place
!+Place:		Nf1-Prop;

LEXICON Nf2-Prop-Place !!≈ - @CODE@ 
+Place:		Nf2-Prop;

LEXICON Nf3-Prop-Place !!≈ - @CODE@ 
+Place:		Nf3-Prop;

LEXICON Nf4-Prop-Place !!≈ - @CODE@ 
+Place:		Nf4-Prop;

LEXICON Nf5-Prop-Place !!≈ - @CODE@ 
+Place:		Nf5-Prop;

LEXICON Nm1-Prop-Place !!≈ - @CODE@ 
+Place:		Nm1-Prop;

! currently not used
!LEXICON Nm2-Prop-Place
!+Place:		Nm2-Prop;

! currently not used
!LEXICON Nm3-Prop-Place
!+Place:		Nm3-Prop;

LEXICON Nm4-Prop-Place !!≈ - @CODE@ 
+Place:		Nm4-Prop;

! currently not used
!LEXICON Nm5-Prop-Place
!+Place:		Nm5-Prop;



LEXICON Nm1-Prop-PName !!≈ - @CODE@ 
+PersName:		Nm1-Prop;

! currently not used
!LEXICON Nm2-Prop-PName
!+PersName:		Nm2-Prop;

LEXICON Nm3-Prop-PName !!≈ - @CODE@ 
+PersName:		Nm3-Prop;

LEXICON Nm4-Prop-PName !!≈ - @CODE@ 
+PersName:		Nm4-Prop;

! currently not used
!LEXICON Nm5-Prop-PName
!+PersName:		Nm5-Prop;

! used for MWE names without inflection
!LEXICON Nf1-Prop-PName
!+PersName:		Nf1-Prop;

LEXICON Nf2-Prop-PName !!≈ - @CODE@ 
+PersName:		Nf2-Prop;

LEXICON Nf3-Prop-PName !!≈ - @CODE@ 
+PersName:		Nf3-Prop;

LEXICON Nf4-Prop-PName !!≈ - @CODE@ 
+PersName:		Nf4-Prop;

! currently not used
!LEXICON Nf5-Prop-PName
!+PersName:		Nf5-Prop;


!! masc nouns - slenderise
LEXICON Nm1-Prop !!≈ - @CODE@ 
+Prop+Noun+Masc+Com:^M^C			Sing;
+Prop+Noun+Masc+Gen:^M^G^Caol	IMGen;
+Prop+Noun+Masc+Voc:^M^V^Caol	Seimh;
+Adj+Base+DeNom:^VHach	NpSéUrú-TIR;

!! fem nouns - slenderise and add e
LEXICON Nf2-Prop !!≈ - @CODE@ 
+Prop+Noun+Fem+Com:^F^C			Sing;
+Prop+Noun+Fem+Gen:^F^G^Caole	IMGen;
+Prop+Noun+Fem+Voc:^F^V			Seimhiu;
+Adj+Base+DeNom:^VHach	NpSéUrú-TIR;


!! fem nouns - broaden and add a
LEXICON Nf3-Prop !!≈ - @CODE@ 
+Prop+Noun+Fem+Com:^F^C			Sing;
+Prop+Noun+Fem+Gen:^F^G^Leaa	IMGen;
+Prop+Noun+Fem+Voc:^F^V		Seimhiu;
+Adj+Base+DeNom:^VHach	NpSéUrú-TIR;

LEXICON Nm3-Prop
+Prop+Noun+Masc+Com:^M^C			Sing;
+Prop+Noun+Masc+Gen:^M^G^Leaa	IMGen;
+Prop+Noun+Masc+Voc:^M^V		Seimhiu;

 
!! fem nouns - no change
LEXICON Nf4-Prop
+Prop+Noun+Fem+Com:^F^C			Sing;
+Prop+Noun+Fem+Gen:^F^G			IMGen;
+Prop+Noun+Fem+Voc:^F^V			Seimhiu;
+Adj+Base+DeNom:^VHach	NpSéUrú-TIR;


!! masc nouns - no change

LEXICON Nm4-Prop
+Prop+Noun+Masc+Com:^M^C			Sing;
+Prop+Noun+Masc+Gen:^M^G		IMGen;
+Prop+Noun+Masc+Voc:^M^V		Seimhiu;
+Adj+Base+DeNom:^VHach	NpSéUrú-TIR;

!! fem nouns - Albain/na hAlban
LEXICON Nf5-Prop
+Prop+Noun+Fem+Com:^F^C			Sing;
+Prop+Noun+Fem+Gen:^F^G^Lea		IMGen;
+Prop+Noun+Fem+Voc:^F^V			Seimhiu;
+Adj+Base+DeNom:^VH^Leaach	NpSéUrú-TIR;


LEXICON IMGen
#;
0:^hv^ts			Seimhiu;

LEXICON Seimh
+Len:^Sé		#;

LEXICON Seimhiu
+Len:^IM^Sé		#;

LEXICON Sing
+Sg:0				#;
+Sg+Len:^IM^Sé			#;
+Sg+Ecl:^IM^Urú			#;
! ??? don't need DefArt with proper nouns - mostly?
!+Sg+DefArt:^Sé^tv^ts		#;

LEXICON NpSéUrú-fem
+Com+Sg:			#; 
+Com+Sg+Len:^IM^Sé		#; 
+Gen+Sg+hPref:^IM^hve		#;  ! muintir na háite
+Com+Sg+Ecl:^IM^Urú		#;
+Gen+Sg+Ecl:^IM^Urú		#;

LEXICON NpSéUrú
+Com+Sg:	#; 
+Com+Sg+Len:^IM^Sé		#; 
+Gen+Sg+Len:^IM^Sé		#; 
+Com+Sg+Ecl:^IM^Urú		#;
+Gen+Sg+Ecl:^IM^Urú		#;

LEXICON NpSéUrú-EN
+Com+Sg:	#; 
+Com+Sg+Len:^IM^Sé		#; 
+Com+Sg+Ecl:^IM^Urú		#;

LEXICON NpSéUrú-PL
+Com+Pl:	#; 
+Com+Pl+Len:^IM^Sé		#; 
+Com+Pl+Ecl:^IM^Urú		#;

! not used
!LEXICON NP-X
!+Prop+Noun:	#;

LEXICON NpSéUrú-TIR
#;
+Len:^IM^Sé		#;
+Ecl:^IM^Urú		#;
+DefArt:^Sé^tv^ts	#;

LEXICON AdjF-Prop
+Adj+Fem+Com+Sg+Len:	#;

! no plural feminine placenemes in lexicon file so far
!LEXICON AdjF-Prop-pl
!+Adj+Fem+Com+Pl:	#;

LEXICON AdjM-Prop
+Adj+Masc+Com+Sg:	#;

LEXICON AdjM-Prop-pl
+Adj+Masc+Com+Pl:	#;


! THIS ONE
!! # Verb (Gaeilge) Replace Rules
!! Elaine Uí Dhonnchadha
! 
!! (c) 2001 - NEW
!! 2005 - new vn/va triggers
!***************************************************************************
!read regex
!! ## PastInd & Imper use stem
regex [i g h -> [%^FT] || _ (%^Caol) %^igh ?* %^Verb [[\%^Fr]|.#.]]  !!≈ - @CODE@ 
.o.
[%^igh ->[]] !!≈ - @CODE@ 
.o.
[a i g h -> [%^FT] || _ %^aigh ?* %^Verb [\%^Fr]] !!≈ - @CODE@ 
.o.
[%^aigh ->[]] !!≈ - @CODE@ 
.o.
!! Verbal Noun Gen
[á i l t e -> %^FT á l a || _ %^áil %^Verb ]  !!≈ - @CODE@ 
.o.
[%^áil ->[]] !!≈ - @CODE@ 
.o.
! Verbal Adjectives
[t h -> [%^FT] || _ %^th %^Verb ] !!≈ - @CODE@ 
.o.
! th -> te 
[%^th ->[]] !!≈ - @CODE@ 
.o.
! bh/gh/mh -> fa
[b h -> [%^FT] ,		 !!≈ - @CODE@ 
g h -> [%^FT] ,		 !!≈ - @CODE@ 
m h -> [%^FT] || _ %^bgmh %^Verb ]		 !!≈ - @CODE@ 
.o.
[%^bgmh ->[]]		 !!≈ - @CODE@ 
.o.

!! ## 2nd. Conjugation Syncopation of Stem

[a i l -> %^FY l || _ %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[a i n -> %^FY n || _ %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[a i r -> %^FY r || _ %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[a i s -> %^FY s || _ %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[i l -> %^FY l || _ %^Caol %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[i n -> %^FY n || _ %^Caol %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[i r -> %^FY r || _ %^Caol %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[i s -> %^FY s || _ %^Caol %^Coim %^Verb [\%^Fr]]		 !!≈ - @CODE@ 
.o.
[%^Coim ->[]]		 !!≈ - @CODE@ 
.o.
[%^Fr ->[]]		 !!≈ - @CODE@ 
.o.

!! ## 1st Conjugation Slender Stems => broaden stems; leave slender for t endings

[%^LC -> %^Caol || _ %^Verb [t|.#.]]		 !!≈ - @CODE@ 
.o.
[i -> [%^FB ] || ?+ _ ? %^LC  %^Verb]		 !!≈ - @CODE@ 
.o.
[%^LC -> 0 ]		 !!≈ - @CODE@ 
.o.

!! ## 1st Conjugation Slender Stems => slenderise endings ! order is important


[a i -> %^FS i || %^Caol %^Verb [f|t]* _ ?+]		 !!≈ - @CODE@ 
.o.
[a í -> %^FS í || %^Caol %^Verb [f|t]* _  ?* .#.]		 !!≈ - @CODE@ 
.o.
[a  -> %^FS e || %^Caol %^Verb [f|t]* _  .#.]		 !!≈ - @CODE@ 
.o.
[á  -> %^FS e á || %^Caol %^Verb [f|t]* _  .#.]		 !!≈ - @CODE@ 
.o.
[a  -> %^FS e a || %^Caol %^Verb [f|t]* _ ?+] 		 !!≈ - @CODE@  f/t - to prevent  amar - eamear
.o.
[ó   -> %^FS e o || %^Caol %^Verb [f|t]* _ ?+] 		 !!≈ - @CODE@ 
.o.
[%^Caol -> []]		 !!≈ - @CODE@ 
.o.

!! ## 1st Conjugation Slender Stems => broaden root eg sábháil

[i -> [%^FB ] || _ ? %^Lea]		 !!≈ - @CODE@ 
.o.
[%^Lea -> []]		 !!≈ - @CODE@ 
.o.
[%^Verb -> []]		 !!≈ - @CODE@ 

.o.

[%^VAdj -> [] ]				 !!≈ - @CODE@ TEMPORARY - VERBAL ADJS. AND VERBAL NOUNS
			!!            IN 2 MUST BE SORTED OUT
;

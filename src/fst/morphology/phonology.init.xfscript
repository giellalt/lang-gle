!THIS ONE
!***************************************************************************
!! # Noun (Gaeilge) Replace Rules
!! ## INITIAL MUTATIONS
!! Elaine Uí Dhonnchadha
!! 12/12/96 - continued March 1998
!! and January 2001
!! - ^Sé : (Séimhiú) Lenition : insert "h" after certain initial consonants
!! - ^Urú : Eclipsis
!! - ^tv & ^hv : Prefix "t-" or "h" to words with an initial vowel
!! - ^ts : prefix "t" to words starting with "s" followed by "l", "n", "r" or a vowel
!! - ^Poss : possessive m', d' etc on nouns
!! Oct 2003
!! INITIAL MUTATION OF 2nd. PART OF COMPOUND
!!
!!***************************************************************************
!! ### DEFINITIONS
!***************************************************************************
define VowelAll a|e|i|o|u|á|é|í|ó|ú|%^AO|%^IA|%^AE|%^UA|A|E|I|O|U|Á|É|Í|Ó|Ú ;		 !!≈ - @CODE@ 
define VowelLC a|e|i|o|u|á|é|í|ó|ú|%^AO|%^IA|%^AE|%^UA ;		 !!≈ - @CODE@ 
!!define Vowel   a|e|i|o|u|á|é|í|ó|ú|%^AO|%^IA|%^AE|%^UA ;
define VowelUC A|E|I|O|U|Á|É|Í|Ó|Ú ;		 !!≈ - @CODE@ 
define Len b|c|d|f|g|m|p|t|B|C|D|F|G|M|P|T ;		 !!≈ - @CODE@ 
define SLen S|s ;		 !!≈ - @CODE@ 
define SWord l|n|r ;		 !!≈ - @CODE@ 
! Séimhiú (Lenite) the following consonants: b,c,d,f,g,m,p,t

!! ### Rules
read regex [..] -> h  || %^CB [m|b|c|d|f|g|p|t] _		 !!≈ - @CODE@ 
.o.
! Séimhiú (Lenite) "s" when followed by [a|e|i|o|u|l|n|r|á|é|í|ó|ú]
[..] -> h || %^CB s _ [VowelLC|l|n|r] 		 !!≈ - @CODE@ 
.o. 
!***************************************************************************
!SÉIMHIÚ (LENITION)
!Noun Fem Com Sg / Noun Masc Gen Sg / Noun Voc /Noun IM / Adj / Verb
!***************************************************************************
! Séimhiú (Lenite) the following consonants: b,c,d,f,g,m,p,t
!!regex [..] -> %^FH h || .#. Len _  ?+ [[%^F %^C]|[%^M %^G]|[%^M %^V]|[%^F %^V]|[%^IM]|[%^Adj]|[%^Verb]|[%^VN]] ?* %^Sé
[..] -> %^FH h || .#. Len _  ?+ [[%^F %^C]|[%^M %^G]|[%^M %^V]|[%^F %^V]|[%^IM]|[%^Adj]|[%^Verb]|[%^VN]] ?* %^Sé		 !!≈ - @CODE@ 
,,		 !!≈ - @CODE@ 
[..] -> %^FH h || .#. [S|s] _  [l|n|r] VowelLC ?+ 		 !!≈ - @CODE@ 
[[%^F %^C]		 !!≈ - @CODE@ 
|[%^M %^G]		 !!≈ - @CODE@ 
|[%^F %^V]		 !!≈ - @CODE@ 
|[%^M %^V]		 !!≈ - @CODE@ 
|[%^IM]		 !!≈ - @CODE@ 
|[%^Adj]		 !!≈ - @CODE@ 
|[%^Verb]		 !!≈ - @CODE@ 
|[%^VN]] 		 !!≈ - @CODE@ 
?* %^Sé		 !!≈ - @CODE@ 
,,		 !!≈ - @CODE@ 
[..] -> %^FH h || .#. [S|s] _  VowelLC ?* [[%^F %^C]|[%^M %^G]|[%^F %^V]|[%^M %^V]|[%^IM]|[%^Adj]|[%^Verb]|[%^VN]] ?* %^Sé		 !!≈ - @CODE@ 
!!.o.
! VERBS ONLY: d' before vowels & f in Past , Imperfect and Conditional of Verbs p97 no.3
! d'fhéach is split into 2 tokens: d' and fhéach EUD 2004
![..] -> d ' || .#. _  [VowelAll|f h] ?+ %^Verb ?* %^Sé

! remove any h prohibited by dntls rule
! e.g. an-saibhir seanduine
! 2014: added %_ for standardised historical forms like aon_duine
.o.
h -> [] || [d|n|t|l|s] ([%-|%_]) %^CB [d|n|t|l|s] _			 !!≈ - @CODE@ 
!!.o.	NOW REMOVED IN n-reg-tidy AT THE ENd
!!%^CB -> []	! initial mutation at compound boundary

.o.
%^Sé -> []		 !!≈ - @CODE@ 
.o.
%^Do -> []		 !!≈ - @CODE@ 
.o.
%^VN -> [] 			 !!≈ - @CODE@  used for séimhiú on f without d' e.g. a fheiceáil not a d'fheiceáil

!***************************************************************************
!URÚ - ECLIPSIS
!***************************************************************************
.o.
[
 b -> %^FU m b ,			 !!≈ - @CODE@  consonants
 B -> %^FU m B ,			 !!≈ - @CODE@  consonants
 c -> %^FU g c ,		 !!≈ - @CODE@ 
 C -> %^FU g C ,		 !!≈ - @CODE@ 
 d -> %^FU n d ,		 !!≈ - @CODE@ 
 D -> %^FU n D ,		 !!≈ - @CODE@ 
 f -> %^FU b h f ,		 !!≈ - @CODE@ 
 F -> %^FU b h F ,		 !!≈ - @CODE@ 
 g -> %^FU n g ,		 !!≈ - @CODE@ 
 G -> %^FU n G ,		 !!≈ - @CODE@ 
 p -> %^FU b p ,		 !!≈ - @CODE@ 
 P -> %^FU b P ,		 !!≈ - @CODE@ 
 T -> %^FU d T ,		 !!≈ - @CODE@ 
 t -> %^FU d t || .#. _ ?+ [[%^G]| [[%^M | %^F] %^C]|[%^Verb]|[%^Adj]|[%^IM]] ?* %^Urú				 !!≈ - @CODE@  Gen Pl & comp prep.
]
.o. 
[..] -> %^FU n %-  || .#. (%^X) _  VowelAll ?* [[%^G]| [[%^M | %^F] %^C]|[%^Verb]|[%^IM]] ?* %^Urú				 !!≈ - @CODE@  Gen Pl only

.o.
[%^Urú -> []]		 !!≈ - @CODE@ 


!***************************************************************************
!! ### VOWEL PREFIXING t-, h, ts
!***************************************************************************
.o.
! t- before Vowel (lowercase) : Masc Com Sg
[..] -> %^FV t %- || .#. (%^X) _ VowelLC ?* %^M %^C ?* %^tv					 !!≈ - @CODE@ 
.o.
! t before Vowel (uppercase) : Masc Com Sg
[..] -> %^FV t || .#. (%^X) _ VowelUC ?* %^M %^C ?* %^tv					 !!≈ - @CODE@ 
.o.
[%^tv -> []]		 !!≈ - @CODE@ 

.o.

! h before Vowel [ Fem Gen Sg | Com Pl]
! ALSO le hÁine, go hiontach
[..] -> %^FV h   || .#. (%^X) _ VowelAll ?* [[%^F %^G] | [%^C]|[%^Verb]|%^IM] ?* %^hv				 !!≈ - @CODE@ 
!.o.
! this inserts hPref on lower not upper side
![0 <- %+hPref || .#. %^FV _ ]
.o.
[%^hv -> []]		 !!≈ - @CODE@ 

.o.
! t before s [ Fem Com Sg | Masc Gen]
 s -> %^FU t s || .#. _  (%^FH) [VowelAll|l|n|r|h] ?* [[%^F %^C]|[%^M %^G]] ?* %^ts		 !!≈ - @CODE@ 

.o.
 S -> %^FU t S || .#. _  (%^FH) [VowelAll|l|n|r|h] ?* [[%^F %^C]|[%^M %^G]] ?* %^ts		 !!≈ - @CODE@ 
.o.
 
%^FH h -> [] || .#. %^FU t [s|S] _ 		 !!≈ - @CODE@ 
!!!h -> [] || .#. %^FU t [s|S] %^FH  _ 

.o. 

%^ts -> []		 !!≈ - @CODE@ 


!***************************************************************************
!! ### POSSESSIVE PREFIX 
!***************************************************************************
.o.

! the following two are now included in determiners with mo and do

![..] -> m '  || .#. _ VowelAll ?+ %^Poss 		! m'athair - my father ; m'aithreacha
!,,
![..] -> d '  || .#. _ VowelAll ?+ %^Poss		! d'athair - your father
!,,

!! the following are performed in parallel as we want all forms

[..] -> %^FV h  || .#. _ VowelAll ?+ %^Poss					 !!≈ - @CODE@  a hathair - her father
,,
[..] -> %^FV n %-  || .#. _ VowelAll ?+ %^Poss 				 !!≈ - @CODE@  n-athair - our/your/their
.o.
%^Poss -> []		 !!≈ - @CODE@ 

.o.
%^IM -> []			 !!≈ - @CODE@  initial mutations caused by compound prepp, possessives etc.

 ;
